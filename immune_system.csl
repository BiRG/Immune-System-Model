PROGRAM

	INITIAL
constant SIMULATION = 1 ! Hall by default

! Initial values blah
constant M0 = 1.75e6
constant DC0 = 4.5e5 
constant NK0 = 340
constant N0 = 6e5
constant Mono0 = 5e5
MonoBlood0 = Mono0
MonoBloodTissue0 = Mono0

constant LuSA = 500 ! cm^2 for mouse (RDDR, EPA)

! Cytokine
constant P50_anti_inflammatory = 100
constant P50_IC_Mono = 20
constant P50_IC_NK = 20

! Natural killer cell
constant krecNK = 0.005        ! basal recruitment, replication/cell division 
constant krecNK_inflammatory = 0.03
constant kdegNK = 0.005
constant kdegNK_anti_inflammatory = 0.04

! Neutrophil
constant krecN = 0.02              ! basal recruitment, replication/cell division 
constant krecN_inflammatory = 0.12
constant P50_IC_N = 160
!constant kdegN_anti_inflammatory = 250
constant kdegN = 0.02
constant kAreaN = 2e-13            ! cm*cm/hr
!constant tau_PN_N = 50

! Pathogens engulfed by neutrophils
!constant kdeathPN = 0.001
constant kLysN = 10
constant P50Lys = 10
constant kRep = 0.693

! Pathogens engulfed by macrophage
constant kdeathPM = 0.05
constant kLys = 10

! Pathogens engulfed by dendritic cells
constant kLysDC = 10

! Monocytes in tissue
! check Goto 2003, Tacke and Randolf 2006 for Mn turnover info - ceh
constant kdegMono = 0               ! Based on half-life
kdiffMonoM = 0.02                   ! 1/time for a monoctye to differentiate into macrophage (t Mono/M)
kdiffMonoDC = 0.02                  ! 1/time for a monoctye to differentiate into dendritic cell (t Mono/DC)

! Monoctyes in blood
krecMonoBlood = 0.04                  ! 1/time for a monocyte to migrate
constant kdegMonoBlood = 0
kmigMonoBlood = krecMonoBlood
constant kmigMonoBlood_inflammatory = 0.1      ! Increase in influx

! Macrophage
constant kdegM = 0
kmigM = kdiffMonoM*Mono0/M0      				! Must be balanced in steady state
kmigMP = 30*kmigM
constant Mvel = 0.02                     ! cm/hr (Stober et al., 1989)
constant MDiam = 15.0e-4                 ! mac diameter (cm)
constant CHEMOI = 8e-8 ! 0.25e2            ! chemotactic index (fit, unitless)
constant kdegMax = 0.01
!constant tau_PM_M = 100
constant P50_rhoP_M = 1500

! IFN-gamma
constant kgenINFG_MP = 0.01 ! IFGN/M/s
constant kdegIFNG = 10

! IL-1A
constant kgenIL1A_MP = 0.1 ! IL-1A/M/s
constant kdegIL1A = 1

! IL-12
constant kgenIL1A_MP = 0.1 ! IL-12/M/s
constant kdegIL12 = 1

! Dendritic cell
kdegDC = 0.9*kdiffMonoDC*Mono0/DC0 		! must be in steady state
constant kAreaDC = 5e-5
kmigDC = 0.1*kdiffMonoDC*Mono0/DC0     ! Must be balanced in steady state
kmigDCA = 30*kmigDC
constant P50_rhoP_DC = 1500

	END ! INITIAL


	DYNAMIC

		ALGORITHM IALG = 5
		NSTEPS    NSTP = 10
		MAXTERVAL MAXT = 1.0e9
		MINTERVAL MINT = 1.0e-9
		CINTERVAL CINT = 0.1

 		DERIVATIVE

TABLE P_experimental_tbl_hall, 1, 3 / 0,24,72,0,1e6,1e8 / ! Hall
TABLE P_experimental_tbl_woolard, 1, 6 / 0,24,72,168,240,336,0,2204.365,81366.88,902036.3,1271972,786.1808 /
TABLE inflammatory_cytokine_tbl_hall, 1, 5 / 0,6,24,72,120,0,32.7,32.6,838.3,171.2 / ! Sharma IFN-gamma
TABLE inflammatory_cytokine_tbl_woolard, 1, 6 / 0,6,24,72,120,350,0,32.7,32.6,193.6,37.9,37.9 / ! Modified Sharma IFN-gamma

IF (SIMULATION .EQ. 1) THEN ! Hall
  P_experimental = P_experimental_tbl_hall(T) 
  IC = inflammatory_cytokine_tbl_hall(T) - inflammatory_cytokine_tbl_hall(0)
ELSE IF (SIMULATION .EQ. 2) THEN ! Woolard
  P_experimental = P_experimental_tbl_woolard(T)
  IC = inflammatory_cytokine_tbl_woolard(T) - inflammatory_cytokine_tbl_woolard(0)
END IF

rhoP = P_experimental/LuSA  ! surface density of pathogen (num/cm**2)	

TABLE anti_inflammatory_cytokine_tbl, 1, 5 / 0,6,24,72,120,300,326.6,304.2,459.7,387.3 /
AIC = anti_inflammatory_cytokine_tbl(T) - anti_inflammatory_cytokine_tbl(0)

PTot = P + PM + PN !+ PDC
PperM = PM/M
!PperDC = PDC/DC
PperN = PN/N

!--------------------------------------------------------------------------
! Dendritic cells
RdeathDC = kdegDC*DC
RmigDC = kmigDC*DC
RrecDC = kdiffMonoDC*Mono
RactDC = 2*P50_rhoP_DC*(1-exp(-rhoP/(P50_rhoP_DC + rhoP)))*kAreaDC*DC ! The exponential term is modeling a saturation
RDC = RrecDC - RmigDC - RdeathDC - RactDC
DC = integ(RDC,DC0)

!--------------------------------------------------------------------------
! Dendritic cells activated by pathogen
RdeathDCA = kdegDC*DCA
RmigDCA = kmigDCA*DCA
DCA = integ(RactDC - RdeathDCA - RmigDCA,0)

!--------------------------------------------------------------------------
! Pathogen engulfed by Dendritic (DC)
! Ignored for now until we get the exact number of infected DC, which is relatively low
!RPhDC = rhoP*kAreaDC*CHEMOI*(DCP+DC)
!RLysDC = kLysDC*PDC
! Ignore replication for now and also assuming when DC dies it doesn't release pathogens
! RRepPDC = kRep*PDC
!RMigPDC = RmigDCP*PDC/max(DCP,1) ! migrating out with the DCs
!PDC = integ(RPhDC - RLysDC - RMigPDC,0)

!--------------------------------------------------------------------------
! Neutrophils
! Cell surface receptors allow neutrophils to detect chemical gradients of cytokines
RrecN = (krecN + krecN_inflammatory*IC/(P50_IC_N + IC))*N0
RdeathN = kdegN*N
RinfN = RPhN*(N/(N+NP))*N ! Compute the fraction of pathogens being engulfed by non-infected
N = integ(-RdeathN + RrecN - RinfN,N0)

!--------------------------------------------------------------------------
! Neutrophil infected by pathogen
RdeathNP = kdegN*NP
NP = integ(RinfN - RdeathNP,0)

!--------------------------------------------------------------------------
! Pathogen engulfed by Neutrophil (N)
RPhN = rhoP*kAreaN*CHEMOI*(NP+N) ! Units: P/s ... pathogens engulfed/s
RLysN = kLysN*PN ! Destroyed by infected neutrophil
! Ignore this for now - Paul
! RRepPN = kRep*PN ! Replicating inside the neutrophil
! RRelN = RdeathN*PN/N
! When an infected neutrophil dies, I'm assuming it is engulfed by macrophage (i.e., it does not release P)
PN = integ(RPhN - RLysN - RdeathNP*PN/max(NP,1),0) 

!--------------------------------------------------------------------------
! PATHOGEN engulfed by MACROPHAGE (PM)
RPhM = 2*P50_rhoP_M*(1-exp(-rhoP/(10*P50_rhoP_M)))*(M + MP)*CHEMOI*Mvel*MDiam ! encounter rate (P/t)
RLys = kLys*PM !min(M,PM)/(PM/max(M,1)+P50Lys)  ! min(M,PM) is estimate of the number of M that have engulfed P.
! Ignored for now
!RRepPM = kRep*PM
!RRelM = RdeathM*PM/M  ! Released from death of M
PM = integ(RPhM - RLys,0)

!--------------------------------------------------------------------------
! Macrophage
RdeathM = kdegM*M
RmigM = kmigM*M
RrecM = kdiffMonoM*Mono
RinfM = RPhM*(M/(M+MP))*M ! Compute the fraction of pathogens being engulfed by non-infected
RM = RrecM - RmigM - RdeathM - RinfM
M = integ(RM,M0)

!--------------------------------------------------------------------------
! Macrophage infected by pathogen
RdeathMP = (kdegM + kdegMax*(1-exp(-PM/max(M,1)/tau_PM_M)))*MP
RmigMP = kmigMP*MP
MP = integ(RinfM - RdeathMP - RmigMP,0)

!--------------------------------------------------------------------------
! IFN-gamma
IFNG = integ(kgenINFG_MP*MP - kdegIFNG*IFNG,0)

!--------------------------------------------------------------------------
! IL-1 alpha
IL1A = integ(kgenIL1A_MP*MP - kdegIL1A*IL1A,0)

!--------------------------------------------------------------------------
! MONOCYTE in blood (MonoBlood)
RrecMonoBlood = krecMonoBlood*MonoBlood0
RdeathMonoBlood = kdegMonoBlood*MonoBlood
RmigMonoBlood = kmigMonoBlood*MonoBlood + kmigMonoBlood_inflammatory*MonoBlood*IC/(P50_IC_Mono + IC)
RMonoBlood = RrecMonoBlood - RdeathMonoBlood - RmigMonoBlood
MonoBlood = integ(RMonoBlood,MonoBlood0)

!--------------------------------------------------------------------------
! MONOCYTE in between blood and tissue (MonoBloodTissue)
RMonoBloodTissue = RmigMonoBlood - RmigMonoBlood/MonoBlood*MonoBloodTissue ! By dividing by the MonoBlood we get a rate constant
MonoBloodTissue = integ(RMonoBloodTissue,MonoBloodTissue0)

!--------------------------------------------------------------------------
! MONOCYTE in tissue (Mono)
RdiffMono = kdiffMonoDC*Mono + kdiffMonoM*Mono
RrecMono = RmigMonoBlood/MonoBlood*MonoBloodTissue ! By dividing by the MonoBlood we get a rate constant
RdeathMono = kdegMono*Mono
RMono = RrecMono - RdiffMono - RdeathMono
Mono = integ(RMono,Mono0)

!--------------------------------------------------------------------------
! Natural killer cells
RdeathNK = kdegNK*NK + kdegNK_anti_inflammatory*AIC/(P50_anti_inflammatory + AIC)*NK
RrecNK = krecNK*NK0 + krecNK_inflammatory*IC/(P50_IC_NK + IC)*NK
NK = integ(RrecNK - RdeathNK,NK0)

!--------------------------------------------------------------------------
! Pathogen
RPh = RPhM + RPhN !+ RPhDC  !+ RPhAT2
RPKillNK = 0
RPKillCTL = 0
RPKill = RPKillNK + RPKillCTL
RRelease = RRelM !+ RRelN
RP = - RPh - RPKill + RRelease
P = integ(RP,0)

		END ! DERIVATIVE

	
		! Add discrete events here as needed
		!  DISCRETE
		!  END

		! code that is executed once at each communication interval goes here

		CONSTANT TSTOP = 10.0
		TERMT (T .GE. TSTOP, 'checked on communication interval: REACHED TSTOP')

	END ! DYNAMIC


	TERMINAL
		! code that is executed once at the end of a simulation run goes here
	END ! TERMINAL

END ! PROGRAM
