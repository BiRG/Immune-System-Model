PROGRAM

	INITIAL
constant SIMULATION = 1 ! Hall by default

! Initial values
constant M0 = 1.75e6
constant DC0 = 4.5e5 
constant NK0 = 340
constant N0 = 6e5
constant Mono0 = 5e5
MonoBlood0 = Mono0
MonoBloodTissue0 = Mono0
constant ATII0 = 2.5e6

constant LuSA = 500 ! cm^2 for mouse (RDDR, EPA)

!-------------------------------------------------------------------------
! Cytokines
! General notes:
!   Monocyte recruitment is controlled by MCP3 - secreted by epithelial cells and monocytes
!   Neutrophil recruitment is driven by GROA - secreted by epithelial cells, t-cells, monocytes, neutrophils, etc
!   NK recruitment is driven by IL1B
!   NK activation (?) is driven by IL18, which is a surrogate for IFN-alpha and IFN-beta
!   IFNG enhanced macrophages and is secreted by NK-cells
constant P50_anti_inflammatory = 100
constant P50_IC_Mono = 20
constant P50_IC_NK = 20

! MCP-3
constant kgenMCP3_Mono = 0.2
constant kgenMCP3_ATII = 0.02

! GROA
constant kgenGROA_Mono = 0.05
constant kgenGROA_ATII = 0.01
constant P50_GROA_N = 800

! IFNG
constant P50_IFNG_M = 100

! Natural killer cell
constant krecNK = 0.005        ! basal recruitment, replication/cell division 
constant krecNK_inflammatory = 0.03
constant kdegNK = 0.005
constant kdegNK_anti_inflammatory = 0.04

! ATII
constant MAX_rhoP_ATII = 5000000
constant kPhATII = 0.2e-13
constant kdegATIIP = 2.7

! Neutrophil
constant krecN = 0.02              ! basal recruitment, replication/cell division 
constant krecN_inflammatory = 0.12
constant P50_IC_N = 160
!constant kdegN_anti_inflammatory = 250
constant kdegN = 0.02
constant kAreaN = 2e-13            ! cm*cm/hr
constant P50_rhoP_N = 3e4
!constant tau_PN_N = 50

! Pathogens engulfed by neutrophils
!constant kdeathPN = 0.001
constant kLysN = 10
constant P50Lys = 10
constant kRep = 0.693

! Pathogens engulfed by macrophage
constant kdeathPM = 0.05
constant kLys = 10

! Pathogens engulfed by dendritic cells
constant kLysDC = 10

! Pathogens enguled by monocytes
constant kLysMono = 10

! Monocytes in tissue
constant kdegMono = 0               ! Based on half-life
kdiffMonoM = 0.02                   ! 1/time for a monoctye to differentiate into macrophage (t Mono/M)
kdiffMonoDC = 0.02                  ! 1/time for a monoctye to differentiate into dendritic cell (t Mono/DC)
constant MAX_rhoP_Mono = 1e5
constant kAreaMono = 1.5e-15

! Monoctyes in blood
krecMonoBlood = 0.04                  ! 1/time for a monocyte to migrate
constant kdegMonoBlood = 0
kmigMonoBlood = krecMonoBlood
constant kmigMonoBlood_inflammatory = -0 ! Increase in influx per MCP-1 or MCP-3
constant MAX_MCP3_Mono = 50

! Macrophage
constant kdegM = 0
kmigM = kdiffMonoM*Mono0/M0      				! Must be balanced in steady state
kmigMP = 30*kmigM
constant Mvel = 0.02                     ! cm/hr (Stober et al., 1989)
constant MDiam = 15.0e-4                 ! mac diameter (cm)
constant CHEMOI = 1e-20 ! 0.25e2            ! chemotactic index (fit, unitless)
constant kdegMax = 0.01
constant P50_rhoP_M = 2e4

! Dendritic cell
kdegDC = 0.9*kdiffMonoDC*Mono0/DC0 		! must be in steady state
constant kAreaDC = 1e-5
kmigDC = 0.1*kdiffMonoDC*Mono0/DC0     ! Must be balanced in steady state
kmigDCA = 35*kmigDC
constant P50_rhoP_DC = 1500

	END ! INITIAL


	DYNAMIC

		ALGORITHM IALG = 5
		NSTEPS    NSTP = 10
		MAXTERVAL MAXT = 1.0e9
		MINTERVAL MINT = 1.0e-9
		CINTERVAL CINT = 0.1

 		DERIVATIVE

TABLE P_experimental_tbl_woolard, 1, 6 / 0,24,72,168,240,336,0,2204.365,81366.88,902036.3,1271972,786.1808 /
TABLE inflammatory_cytokine_tbl_hall, 1, 5 / 0,6,24,72,120,0,32.7,32.6,838.3,171.2 / ! Sharma IFN-gamma
TABLE inflammatory_cytokine_tbl_woolard, 1, 6 / 0,6,24,72,120,350,0,32.7,32.6,193.6,37.9,37.9 / ! Modified Sharma IFN-gamma
INCLUDE 'TBL.csl'

IF (SIMULATION .EQ. 1) THEN ! Hall
  IC = inflammatory_cytokine_tbl_hall(T) - inflammatory_cytokine_tbl_hall(0)
ELSE IF (SIMULATION .EQ. 2) THEN ! Woolard
  IC = inflammatory_cytokine_tbl_woolard(T) - inflammatory_cytokine_tbl_woolard(0)
END IF

rhoP = P_experimental/LuSA  ! surface density of pathogen (num/cm**2)	

TABLE anti_inflammatory_cytokine_tbl, 1, 5 / 0,6,24,72,120,300,326.6,304.2,459.7,387.3 /
AIC = anti_inflammatory_cytokine_tbl(T) - anti_inflammatory_cytokine_tbl(0)

PTot = P + PM + PN !+ PDC
PperM = PM/M
!PperDC = PDC/DC
PperN = PN/N

!--------------------------------------------------------------------------
! Cytokines
!--------------------------------------------------------------------------

include 'MCP3.csl'
include 'GROA.csl'

!--------------------------------------------------------------------------
! Cells
!--------------------------------------------------------------------------

include 'ATII.csl'
include 'DC.csl'
INCLUDE 'M.csl'
INCLUDE 'Mono.csl'
INCLUDE 'N.csl'

!--------------------------------------------------------------------------
! Natural killer cells
RdeathNK = kdegNK*NK + kdegNK_anti_inflammatory*AIC/(P50_anti_inflammatory + AIC)*NK
RrecNK = krecNK*NK0 + krecNK_inflammatory*IC/(P50_IC_NK + IC)*NK
NK = integ(RrecNK - RdeathNK,NK0)

!--------------------------------------------------------------------------
! Pathogen
RPh = RPhM + RPhN !+ RPhDC  !+ RPhAT2
RPKillNK = 0
RPKillCTL = 0
RPKill = RPKillNK + RPKillCTL
RRelease = 0!RRelM !+ RRelN
RP = - RPh - RPKill + RRelease
P = integ(RP,0)

		END ! DERIVATIVE

	
		! Add discrete events here as needed
		!  DISCRETE
		!  END

		! code that is executed once at each communication interval goes here

		CONSTANT TSTOP = 10.0
		TERMT (T .GE. TSTOP, 'checked on communication interval: REACHED TSTOP')

	END ! DYNAMIC


	TERMINAL
		! code that is executed once at the end of a simulation run goes here
	END ! TERMINAL

END ! PROGRAM
